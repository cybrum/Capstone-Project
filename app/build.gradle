apply plugin: 'com.android.application'

// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        applicationId "com.udacity.nanodegree.mystockhealth"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        shrunk {
            minifyEnabled true
            shrinkResources true
        }
        big {
            minifyEnabled false
            shrinkResources false
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

}

repositories {
    maven { url 'https://jitpack.io' }
    //https://github.com/firebase/FirebaseUI-Android/issues/392' 
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.afollestad.material-dialogs:core:0.9.1.0'
    compile 'com.google.android.gms:play-services-gcm:9.8.0'
    compile 'net.simonvt.schematic:schematic-compiler:0.6.8'
    compile 'net.simonvt.schematic:schematic:0.6.8'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.android.support:support-v4:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'
    compile 'com.github.lecho:hellocharts-library:1.5.8@aar'
    compile 'com.android.support:gridlayout-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:appcompat-v7:25.0.1'
    // Firebase
    compile 'com.google.firebase:firebase-database:9.8.0'
    compile 'com.google.firebase:firebase-auth:9.8.0'
    // FirebaseUI Database only
    //compile 'com.firebaseui:firebase-ui-database:1.0.0'
    // Single target that includes all FirebaseUI libraries
    //1.0.0 needs com.twitter.sdk.android:twitter:2.0.0.
    compile 'com.firebaseui:firebase-ui-auth:0.6.2'
}
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.google.gms.google-services'